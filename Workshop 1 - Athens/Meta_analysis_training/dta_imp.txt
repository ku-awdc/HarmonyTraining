model {

	for(i in 1:l) {
		# Likelihood	
		# se, sp are accuracy of CI
		# s2, c2 are accuracy of LU
		# pi is the prevalence

		cell[i,1:4] ~ dmulti(prob[i,1:4],n[i])
	
		prob[i,1] <- pi[i]*se[i]*s2+(1-pi[i])*(1-sp[i])*(1-c2)
		prob[i,2] <- pi[i]*se[i]*(1-s2)+(1-pi[i])*(1-sp[i])*c2
		prob[i,3] <- pi[i]*(1-se[i])*s2+(1-pi[i])*sp[i]*(1-c2)
		prob[i,4] <- pi[i]*(1-se[i])*(1-s2)+(1-pi[i])*sp[i]*c2

		# Expressing accuracy in terms of HSROC parameters

		b[i] <- exp((beta)/2)
		logit(se[i]) <- (theta[i] + 0.5*alpha[i])/b[i]
		logit(sp[i]) <- -(theta[i] - 0.5*alpha[i])*b[i]

		# Priors for CI accuracy

		theta[i] ~ dnorm(THETA,prec[1])
		alpha[i] ~ dnorm(LAMBDA,prec[2]) 


		# Priors for prevalence parameters

		pi[i] ~ dbeta(1,1)


	}

	# CI accuracy
	
	Se_overall <- 1/(1+exp((-THETA-0.5*LAMBDA)/exp(beta/2)))
	Sp_overall <- 1/(1+exp((THETA-0.5*LAMBDA)*exp(beta/2)))		
	
	theta_new ~ dnorm(THETA,prec[1])
	alpha_new ~ dnorm(LAMBDA,prec[2])
	

	# Predicted values for CI in a new study

	Se_new <- 1/(1+exp(-(theta_new+0.5*alpha_new)/exp(beta/2)))
	Sp_new <- 1/(1+exp((theta_new-0.5*alpha_new)*exp(beta/2)))


	# Priors over the accuracy parameters of CI

	THETA ~ dunif(-2.6,2.6) 
	LAMBDA ~ dunif(-5.2,5.2)
	beta ~ dunif(-1.3,1.3)

	for(j in 1:2) {
		
			prec[j] <- pow(sigma[j],-2) 
			sigma[j] ~ dgamma(4,2)	
	}

	# Priors over the accuracy parameters of the imperfect reference test (LU)

		###	Informative Priors	###
		s2 ~ dbeta(36.279, 13.761);
		c2 ~ dbeta(67.181, 9.597);

		###	Non-Informative 	###
		#s2 ~ dbeta(1,1);
		#c2 ~ dbeta(1,1);

	#data# l, n, cell
	#inits# LAMBDA, THETA, beta, pi, sigma
	#monitor# c2, s2, Se_overall ,Sp_overall


}